# Kernel: sgemm_nn_128x32

# Copyright 2014 Nervana Systems Inc. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#    http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


<CONSTANT_MAPPING>
    addr_zero  : 4x<128*8*2 + 32*8*2 + 0>

    gridDimA : c[0x0][0x14]
    gridDimB : c[0x0][0x18]

    param_Rand[0]   : c[0x0][0x140]
    param_Rand[1]   : c[0x0][0x144]
    param_A[0]      : c[0x0][0x148]
    param_A[1]      : c[0x0][0x14c]
    param_B[0]      : c[0x0][0x150]
    param_B[1]      : c[0x0][0x154]
    param_C[0]      : c[0x0][0x158]
    param_C[1]      : c[0x0][0x15c]
    param_lda       : c[0x0][0x160]
    param_ldb       : c[0x0][0x164]
    param_ldc       : c[0x0][0x168]
    param_m         : c[0x0][0x16c]
    param_n         : c[0x0][0x170]
    param_k         : c[0x0][0x174]
    param_alpha     : c[0x0][0x178]
    param_beta      : c[0x0][0x17c]
    param_flags     : c[0x0][0x180]
</CONSTANT_MAPPING>

<REGISTER_MAPPING>

    0-31 ~ tidAX, tidBX, lda, ldb, tid1, tid2, tid7, txa00, txa64, ta, xmad_ta, tb, tid15

    0-31 : czero<00-31>

     3, 2,11,10 : cx<0-3>y0
     7, 6,15,14 : cx<0-3>y1
     1, 0, 9, 8 : cx<0-3>y2
     5, 4,13,12 : cx<0-3>y3
    19,18,27,26 : cx<0-3>y4
    23,22,31,30 : cx<0-3>y5
    17,16,25,24 : cx<0-3>y6
    21,20,29,28 : cx<0-3>y7

    32-55  ~ k<1-3>, x1
 
    32-43  : j0Ay<0-7>, j0Bx<0-3>
    44-55  : j1Ay<0-7>, j1Bx<0-3>
 
    56-73  : loadAA<0-7>, loadA<0-7>, loadB<0-1>
 
    74-79  : track0A<0-1>, track1A<0-1>, trackB<0-1>

    80-127 ~ writeAs, writeBs, k, ldb8, swapBuf, readAs, readBs, tid, blkA, blkB, txb, tidAY, tidBY, ge16

    32-39 : c<0-3>, d3, d2, d1, d0
    40-47 : C00y<0-1>, C04y<0-1>, C08y<0-1>, C12y<0-1>
    48-79 ~ ldc, ldc1, ldc4, ldc60, writeCs, readCs, cx, cy<00|04|08|12>, ci, xmad_c, alpha, beta, flags, tid31, tid96

</REGISTER_MAPPING>

--:-:1:-:1      S2R tid,  SR_TID.X;
--:-:2:-:1      S2R blkA, SR_CTAID.X;
--:-:3:-:1      S2R blkB, SR_CTAID.Y;

<SCHEDULE_BLOCK>
--:-:-:-:1      MOV k,   param_k;
--:-:-:-:1      MOV lda, param_lda;
--:-:-:-:1      MOV ldb, param_ldb;
--:-:-:-:1      SHL ldb8, ldb, 5;
--:-:-:-:1      MOV32I ge16, 0xfffffff0;

--:-:-:-:1      STS.128 [addr_zero], RZ;

// tidAY = (tid & 1) << 2
// tidAX = tid >> 1
01:-:-:-:1      LOP.AND tid1,  tid,  1;
--:-:-:-:1      SHL     tidAY, tid1, 2;
01:-:-:-:1      SHR.U32 tidAX, tid,  1;

// trackA += 4 * ((blkA*128 + tidAX) * lda + tidAY)
02:-:-:-:1      ISCADD  txa00, blkA, tidAX, 7;
--:-:-:-:1      XMAD.LO ta, lda, txa00, tidAY, xmad_ta;
--:-:-:-:1      LEA      track0A0.CC, ta, param_A[0],     2;
--:-:-:-:1      LEA.HI.X track0A1,    ta, param_A[1], RZ, 2;
--:-:-:-:1      LEA      track1A0.CC, lda, track0A0,      8;
--:-:-:-:1      LEA.HI.X track1A1,    lda, track0A1, RZ,  8;
--:-:-:-:1      IADD txa64, txa00, 64;

--:-:-:-:1      ISETP.LT.AND P4, PT, txa00, param_m, PT;
--:-:-:-:1      ISETP.LT.AND P5, PT, txa64, param_m, PT;

// tidBX = (tid & 15) << 1
// tidBY = (tid >> 4) & 7
--:-:-:-:1      LOP.AND tid15, tid,   15;
--:-:-:-:1      SHL     tidBX, tid15, 1;
--:-:-:-:1      BFE.U32 tidBY, tid,   0x304; // 3 bits at position 4

// trackB += (blkB*32 + ldb*tidBY + tidBX) * 4
04:-:-:-:1      ISCADD   txb, blkB, tidBX, 5;
--:-:-:-:1      XMAD.LO2 tb,  ldb,  tidBY, txb;
--:-:-:-:1      LEA      trackB0.CC, tb, param_B[0],     2;
--:-:-:-:0      LEA.HI.X trackB1,    tb, param_B[1], RZ, 2;

--:-:-:-:1      ISETP.LT.AND P6, PT, txb, param_n, PT;

// Start the write buffers high
// writeAs = (128*tidAY + tidAX) * 4
--:-:-:-:1      ISCADD writeAs, tidAY, tidAX, 7;
--:-:-:-:1      ISCADD writeAs, writeAs, 4x<32*8 + 128*8>, 2;

// writeBs = (32*tidBY + tidX) * 4
--:-:-:-:1      ISCADD writeBs, tidBY, tidBX, 5;
--:-:-:-:1      ISCADD writeBs, writeBs, 4x<32*8 + 128*8*2>, 2;

// Start the read buffers low
// readAs  = (((tid & 0x70) >> 3) | (tid & 1)) << 4;
--:-:-:-:1      LOP.AND readAs, tid,    0x70;
--:-:-:-:1      SHR.U32 readAs, readAs, 3;
--:-:-:-:1      LOP.OR  readAs, readAs, tid1;
--:-:-:-:1      SHL     readAs, readAs, 4;
// readBs = ((tid >> 1) & 7) << 4 + 4x<128*8>;
--:-:-:-:1      BFE.U32 readBs, tid,    0x301; // 3 bits at position 1
--:-:-:-:1      ISCADD  readBs, readBs, 4x<128*8>, 4;

--:-:-:-:1      MOV32I swapBuf, -4x<32*8 + 128*8>;
</SCHEDULE_BLOCK>

<CODE>
    return join '', map sprintf("--:-:1:-:1      LDS.U.128 czero%02d, [addr_zero];\n", $_ * 4), 0..7;
</CODE>
//<CODE>
//   return join '', map sprintf("--:-:-:-:1      MOV czero%02d, RZ;\n", $_ ), 0..31;
//</CODE>

REMAINDER:

<CODE>
    our $vec;
    return $vec ? q{

// k must be multiple of 8
--:-:2:-:1  @P6 LDG.E.CI.64  loadB0,  [trackB];
 
--:-:3:-:1  @P4 LDG.E.CI.128 loadA0,  [track0A + 4x<0>];
--:-:3:-:1  @P4 LDG.E.CI.128 loadAA0, [track0A + 4x<8>];

--:-:4:-:1  @P5 LDG.E.CI.128 loadA4,  [track1A + 4x<0>];
--:-:4:-:1  @P5 LDG.E.CI.128 loadAA4, [track1A + 4x<8>];

--:-:-:-:1 @!P6 LDS.U.64  loadB0,  [addr_zero];
--:-:-:-:1 @!P4 LDS.U.128 loadA0,  [addr_zero];
--:-:6:-:1 @!P5 LDS.U.128 loadA4,  [addr_zero];
--:-:-:-:1 @!P4 LDS.U.128 loadAA0, [addr_zero];
--:-:-:-:1 @!P5 LDS.U.128 loadAA4, [addr_zero];

--:-:-:-:0      PSETP.AND.AND P1, PT, PT, PT, PT;

22:-:-:-:1      STS.64 [writeBs], loadB0;

--:-:-:-:6      IADD   trackB0.CC, trackB0, ldb8;
--:-:-:-:0      IADD.X trackB1, trackB1, RZ;

04:-:-:-:1      STS [writeAs + 4x<0*128 + 00>], loadA0;
--:-:-:-:0      IADD   track0A0.CC, track0A0, 4x<16>;
--:-:-:-:1      STS [writeAs + 4x<1*128 + 00>], loadA1;
--:-:-:-:1      STS [writeAs + 4x<2*128 + 00>], loadA2;
--:-:-:-:1      STS [writeAs + 4x<3*128 + 00>], loadA3;

08:-:-:-:1      STS [writeAs + 4x<0*128 + 64>], loadA4;
--:-:-:-:1      STS [writeAs + 4x<1*128 + 64>], loadA5;
--:-:-:-:1      STS [writeAs + 4x<2*128 + 64>], loadA6;
--:-:-:-:0      IADD.X track0A1,    track0A1, RZ;
--:-:-:-:1      STS [writeAs + 4x<3*128 + 64>], loadA7;

--:-:-:-:1      IADD   track1A0.CC, track1A0, 4x<16>;

--:-:-:-:1      IADD readBs,  readBs, -swapBuf;
--:-:-:-:0      IADD readAs,  readAs, -swapBuf;
01:-:-:-:5      BAR.SYNC 0;
--:-:-:-:1      IADD writeBs, writeBs, swapBuf;
--:-:-:-:1      IADD writeAs, writeAs, swapBuf;
--:-:-:-:1      IADD swapBuf, RZ, -swapBuf;

--:-:-:-:0      IADD.X track1A1,    track1A1, RZ;


    } : q{

<SCHEDULE_BLOCK>

--:-:-:-:1      ISETP.LT.AND P6, PT, txb, param_n, PT;

// doLoad0 = tidBY < k
--:-:-:-:1      IADD x1, txb, 1;

--:-:-:-:1      ISETP.LT.AND P0, PT, tidBY, k, P6;
--:-:-:-:1      ISETP.LT.AND P1, PT, x1, param_n, P0;
<ORDERED>
--:-:2:-:1  @P0 LDG.E.CI loadB0, [trackB + 4x<0>];
--:-:2:-:1  @P1 LDG.E.CI loadB1, [trackB + 4x<1>];
</ORDERED>
--:-:-:-:1 @!P0 MOV loadB0, RZ;
--:-:-:-:1 @!P1 MOV loadB1, RZ;

--:-:-:-:1      IADD k1, tidAY, 1;
--:-:-:-:1      IADD k2, tidAY, 2;
--:-:-:-:1      IADD k3, tidAY, 3;

--:-:-:-:1      ISETP.LT.AND P0, PT, tidAY, k, P4;
--:-:-:-:1      ISETP.LT.AND P1, PT, k1, k, P4;
--:-:-:-:1      ISETP.LT.AND P2, PT, k2, k, P4;
--:-:-:-:1      ISETP.LT.AND P3, PT, k3, k, P4;
<ORDERED>
--:-:3:-:1  @P0 LDG.E.CI loadA0, [track0A + 4x<0>];
--:-:3:-:1  @P1 LDG.E.CI loadA1, [track0A + 4x<1>];
--:-:3:-:1  @P2 LDG.E.CI loadA2, [track0A + 4x<2>];
--:-:3:-:1  @P3 LDG.E.CI loadA3, [track0A + 4x<3>];
</ORDERED>
--:-:-:-:1 @!P0 MOV loadA0, RZ;
--:-:-:-:1 @!P1 MOV loadA1, RZ;
--:-:-:-:1 @!P2 MOV loadA2, RZ;
--:-:-:-:1 @!P3 MOV loadA3, RZ;

--:-:-:-:1      ISETP.LT.AND P0, PT, tidAY, k, P5;
--:-:-:-:1      ISETP.LT.AND P1, PT, k1, k, P5;
--:-:-:-:1      ISETP.LT.AND P2, PT, k2, k, P5;
--:-:-:-:1      ISETP.LT.AND P3, PT, k3, k, P5;
<ORDERED>
--:-:4:-:1  @P0 LDG.E.CI loadA4, [track1A + 4x<0>];
--:-:4:-:1  @P1 LDG.E.CI loadA5, [track1A + 4x<1>];
--:-:4:-:1  @P2 LDG.E.CI loadA6, [track1A + 4x<2>];
--:-:4:-:1  @P3 LDG.E.CI loadA7, [track1A + 4x<3>];
</ORDERED>
--:-:-:-:1 @!P0 MOV loadA4, RZ;
--:-:-:-:1 @!P1 MOV loadA5, RZ;
--:-:-:-:1 @!P2 MOV loadA6, RZ;
--:-:-:-:1 @!P3 MOV loadA7, RZ;
</SCHEDULE_BLOCK>

02:-:-:-:1      STS.64 [writeBs], loadB0;

--:-:-:-:6      IADD   trackB0.CC, trackB0, ldb8;
--:-:-:-:0      IADD.X trackB1, trackB1, RZ;

04:-:-:-:1      STS [writeAs + 4x<0*128 + 00>], loadA0;
--:-:-:-:1      STS [writeAs + 4x<1*128 + 00>], loadA1;
--:-:-:-:1      STS [writeAs + 4x<2*128 + 00>], loadA2;
--:-:-:-:1      STS [writeAs + 4x<3*128 + 00>], loadA3;

--:-:-:-:6      IADD   track0A0.CC, track0A0, 4x<8>;
--:-:-:-:0      IADD.X track0A1,    track0A1, RZ;

08:-:-:-:1      STS [writeAs + 4x<0*128 + 64>], loadA4;
--:-:-:-:1      STS [writeAs + 4x<1*128 + 64>], loadA5;
--:-:-:-:1      STS [writeAs + 4x<2*128 + 64>], loadA6;
--:-:-:-:1      STS [writeAs + 4x<3*128 + 64>], loadA7;

--:-:-:-:6      IADD   track1A0.CC, track1A0, 4x<8>;
--:-:-:-:1      IADD.X track1A1,    track1A1, RZ;

--:-:-:-:1      IADD readBs,  readBs, -swapBuf;
--:-:-:-:0      IADD readAs,  readAs, -swapBuf;
01:-:-:-:5      BAR.SYNC 0;
--:-:-:-:1      IADD writeBs, writeBs, swapBuf;
--:-:-:-:1      IADD writeAs, writeAs, swapBuf;
--:-:-:-:1      IADD swapBuf, RZ, -swapBuf;

--:-:-:-:0      ISETP.GT.AND P1, PT, k, 8, PT;

    };
</CODE>

<CODE>
    our $vec;
    our @top = $vec ? (
        # cute way to avoid stringing together 3 half throughput instructions
        "--:-:-:-:1      ISETP.GE.AND P6, PT, k, 16, P6;\n",
        "--:-:-:-:1      LOP.AND.NZ P0, RZ, k, ge16;\n",
        "--:-:-:-:1      PSETP.AND.AND P1, PT, !P1, PT, PT;\n", 
    ) : (
        "--:-:-:-:2      ISETP.GE.AND P6, PT, k, 24, P6;\n",
        "--:-:-:-:1      ISETP.GE.AND P0, PT, k, 24, PT;\n",
    );
    our %insert =
    (
        j0c6  => "--:-:-:-:1      IADD32I k, k, -8;\n",

        ($vec ? 
            (
        j0c9  => "--:-:2:-:1  \@P6 LDG.E.CI.64 loadB0, [trackB];\n",

        j0c10 => "--:-:-:-:1      PSETP.AND.AND P2, PT, P0, P1, P4;\n",
        j0c11 => "--:-:-:-:1      PSETP.AND.AND P3, PT, P0, P1, P5;\n",

        j0c23 => "--:-:3:-:1  \@P2 LDG.E.CI.128 loadA0,  [track0A + 4x<0>];\n",
        j0c25 => "--:-:3:-:1  \@P2 LDG.E.CI.128 loadAA0, [track0A + 4x<8>];\n",
        
        j0c27 => "--:-:4:-:1  \@P3 LDG.E.CI.128 loadA4,  [track1A + 4x<0>];\n",
        j0c29 => "10:6:5:-:1  \@P3 LDG.E.CI.128 loadAA4, [track1A + 4x<8>];\n",

        j3c13 => "--:-:-:-:1 \@!P1 STS [writeAs + 4x<0*128 + 00>], loadAA0;\n",
        j3c15 => "--:-:-:-:1 \@!P1 STS [writeAs + 4x<1*128 + 00>], loadAA1;\n",
        j3c17 => "--:-:-:-:1 \@!P1 STS [writeAs + 4x<2*128 + 00>], loadAA2;\n",
        j3c19 => "--:-:-:-:1 \@!P1 STS [writeAs + 4x<3*128 + 00>], loadAA3;\n",

        j4c13 => "--:-:-:-:1 \@!P1 STS [writeAs + 4x<0*128 + 64>], loadAA4;\n",
        j4c15 => "--:-:-:-:1 \@!P1 STS [writeAs + 4x<1*128 + 64>], loadAA5;\n",
        j4c17 => "--:-:-:-:1 \@!P1 STS [writeAs + 4x<2*128 + 64>], loadAA6;\n",
        j4c19 => "--:-:-:-:1 \@!P1 STS [writeAs + 4x<3*128 + 64>], loadAA7;\n",

        j6c8  => "04:-:-:-:1  \@P1 STS [writeAs + 4x<0*128 + 00>], loadA0;\n",
        j6c10 => "--:-:-:-:1  \@P1 STS [writeAs + 4x<1*128 + 00>], loadA1;\n",
        j6c12 => "--:-:-:-:1  \@P1 STS [writeAs + 4x<2*128 + 00>], loadA2;\n",
        j6c14 => "--:-:-:-:1  \@P1 STS [writeAs + 4x<3*128 + 00>], loadA3;\n",

        j6c16 => "08:-:-:-:1  \@P1 STS [writeAs + 4x<0*128 + 64>], loadA4;\n",
        j6c18 => "--:-:-:-:1  \@P1 STS [writeAs + 4x<1*128 + 64>], loadA5;\n",
        j6c20 => "--:-:-:-:1  \@P1 STS [writeAs + 4x<2*128 + 64>], loadA6;\n",
        j6c22 => "--:-:-:-:1  \@P1 STS [writeAs + 4x<3*128 + 64>], loadA7;\n",

        j6c15 => "--:-:-:-:1  \@P2 IADD   track0A0.CC, track0A0, 4x<16>;\n",
        j6c21 => "--:-:-:-:1  \@P2 IADD.X track0A1,    track0A1, RZ;\n",

        j6c23 => "20:-:-:-:1  \@P3 IADD   track1A0.CC, track1A0, 4x<16>;\n",
        j6c29 => "--:-:-:-:1  \@P3 IADD.X track1A1,    track1A1, RZ;\n",

        j7c31 => "--:-:-:Y:5  \@P0 BRA.U LOOP;\n",
            ) :
            (
        j0c1  => "--:-:-:-:1      ISETP.GE.AND P2, PT, k, 24, P4;\n",
        j0c3  => "--:-:-:-:1      ISETP.GE.AND P3, PT, k, 24, P5;\n",

        j0c8  => "--:-:2:-:1  \@P6 LDG.E.CI loadB0, [trackB + 4x<0>];\n",
        j0c10 => "--:-:2:-:1  \@P6 LDG.E.CI loadB1, [trackB + 4x<1>];\n",

        j0c12 => "--:-:3:-:1  \@P2 LDG.E.CI loadA0, [track0A + 4x<0>];\n",
        j0c14 => "--:-:3:-:1  \@P2 LDG.E.CI loadA1, [track0A + 4x<1>];\n",
        j0c16 => "--:-:3:-:1  \@P2 LDG.E.CI loadA2, [track0A + 4x<2>];\n",
        j0c18 => "--:-:3:-:1  \@P2 LDG.E.CI loadA3, [track0A + 4x<3>];\n",

        j0c20 => "--:-:4:-:1  \@P3 LDG.E.CI loadA4, [track1A + 4x<0>];\n",
        j0c22 => "--:-:4:-:1  \@P3 LDG.E.CI loadA5, [track1A + 4x<1>];\n",
        j0c24 => "--:-:4:-:1  \@P3 LDG.E.CI loadA6, [track1A + 4x<2>];\n",
        j0c26 => "--:-:4:-:1  \@P3 LDG.E.CI loadA7, [track1A + 4x<3>];\n",

        j6c8  => "04:-:-:-:1  \@P0 STS [writeAs + 4x<0*128 + 00>], loadA0;\n",
        j6c10 => "--:-:-:-:1  \@P0 STS [writeAs + 4x<1*128 + 00>], loadA1;\n",
        j6c12 => "--:-:-:-:1  \@P0 STS [writeAs + 4x<2*128 + 00>], loadA2;\n",
        j6c14 => "--:-:-:-:1  \@P0 STS [writeAs + 4x<3*128 + 00>], loadA3;\n",

        j6c16 => "08:-:-:-:1  \@P0 STS [writeAs + 4x<0*128 + 64>], loadA4;\n",
        j6c18 => "--:-:-:-:1  \@P0 STS [writeAs + 4x<1*128 + 64>], loadA5;\n",
        j6c20 => "--:-:-:-:1  \@P0 STS [writeAs + 4x<2*128 + 64>], loadA6;\n",
        j6c22 => "--:-:-:-:1  \@P0 STS [writeAs + 4x<3*128 + 64>], loadA7;\n",

        j6c15 => "--:-:-:-:1  \@P2 IADD   track0A0.CC, track0A0, 4x<8>;\n",
        j6c21 => "--:-:-:-:1  \@P2 IADD.X track0A1,    track0A1, RZ;\n",

        j6c23 => "--:-:-:-:1  \@P3 IADD   track1A0.CC, track1A0, 4x<8>;\n",
        j6c29 => "--:-:-:-:1  \@P3 IADD.X track1A1,    track1A1, RZ;\n",

        j7c31 => "--:-:-:Y:5  \@P0 BRA.U LOOP;\n" .
                 "--:-:-:Y:5  \@P1 BRA.U REMAINDER;\n",
            )
        ),
        j6c6  => "02:-:-:-:1  \@P0 STS.64 [writeBs], loadB0;\n",

        j6c7  => "--:-:-:-:1  \@P0 IADD   trackB0.CC, trackB0, ldb8;\n",
        j6c13 => "--:-:-:-:1  \@P0 IADD.X trackB1,    trackB1, RZ;\n",


        j6c31 => "--:-:-:-:5  \@P0 BAR.SYNC 0;\n" .
                 "--:-:-:-:1  \@P0 IADD readBs,  readBs, -swapBuf;\n" .
                 "--:-:-:-:1  \@P0 IADD readAs,  readAs, -swapBuf;\n" .
                 "--:-:-:-:1  \@P0 IADD writeBs, writeBs, swapBuf;\n" .
                 "--:-:-:-:1  \@P0 IADD writeAs, writeAs, swapBuf;\n" .
                 "--:-:-:-:1  \@P0 IADD swapBuf, RZ,     -swapBuf;\n",


    );
    return;
</CODE>

<INCLUDE file="nervanagpu/kernels/sass/sgemm_common_128x32.sass"/>