# Kernel: hgemm_tn_128x128

# Copyright 2014 Nervana Systems Inc. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#    http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


<CONSTANT_MAPPING>
    addr_zero  : 4x<128*8*4 + 0>

    gridDimA : c[0x0][0x14]
    gridDimB : c[0x0][0x18]

    param_Rand[0]   : c[0x0][0x140]
    param_Rand[1]   : c[0x0][0x144]
    param_A[0]      : c[0x0][0x148]
    param_A[1]      : c[0x0][0x14c]
    param_B[0]      : c[0x0][0x150]
    param_B[1]      : c[0x0][0x154]
    param_C[0]      : c[0x0][0x158]
    param_C[1]      : c[0x0][0x15c]
    param_lda       : c[0x0][0x160]
    param_ldb       : c[0x0][0x164]
    param_ldc       : c[0x0][0x168]
    param_m         : c[0x0][0x16c]
    param_n         : c[0x0][0x170]
    param_k         : c[0x0][0x174]
    param_alpha     : c[0x0][0x178]
    param_beta      : c[0x0][0x17c]
    param_flags     : c[0x0][0x180]
</CONSTANT_MAPPING>

<REGISTER_MAPPING>

    0-63    ~ lda, ldb, tid1, ta, tb, tid7, tid15, tidX, blk, dimA, flag

    0-63    : czero<00-63>

     3, 2,11,10,19,18,27,26 : cx<0-7>y0
     7, 6,15,14,23,22,31,30 : cx<0-7>y1
     1, 0, 9, 8,17,16,25,24 : cx<0-7>y2
     5, 4,13,12,21,20,29,28 : cx<0-7>y3
    35,34,43,42,51,50,59,58 : cx<0-7>y4
    39,38,47,46,55,54,63,62 : cx<0-7>y5
    33,32,41,40,49,48,57,56 : cx<0-7>y6
    37,36,45,44,53,52,61,60 : cx<0-7>y7

    64-95   ~ x<1-3>, y<1-3>

    80-81   : Rand<0-1>

    64-79   : j0Ay<0-7>, j0Bx<0-7>
    80-95   : j1Ay<0-7>, j1Bx<0-7>

    96-103  : loadX<0-7>
    96-103  : loadA<0-3>, loadB<0-3>

    104-107 : trackX<0-1>
    104-107 : trackA<0-1>, trackB<0-1>

    108-125 ~ writeS, readAs, readBs, k, lda8, ldb8, tid, txa, txb, tid31, tid96, tid128, blkA, blkB, tidY
    126-127 ~ tbid, seed

    64-75   : c<0-7>, d3, d2, d1, d0
    76-85   : C00y<0-1>, C04y<0-1>, C08y<0-1>, C12y<0-1>
    86-107  ~ lfsr<0-2>, ldc, ldc1, ldc4, ldc60, writeCs, readCs, cx<00|64>, cy<00|04|08|12>, ci, xmad_c, alpha, beta, clk_shf1, clk_shf2

    108-125 ~ exp<0-3>, rand<0-3>, lfsr<0-2>_1, lfsr<0-2>_2, flags

</REGISTER_MAPPING>

--:-:1:-:1      S2R tid,  SR_TID.X;
--:-:2:-:1      S2R blkA, SR_CTAID.X;
--:-:3:-:1      S2R blkB, SR_CTAID.Y;

<SCHEDULE_BLOCK>
--:-:-:-:1      MOV k,    param_k;
--:-:-:-:1      STS.128 [addr_zero], RZ;

// Grab a seed for this thread
// (blkB*gridDimA*256 + blkA*256 + tid) & (1024*256 - 1)
--:-:-:-:1      MOV flag, param_flags;
--:-:-:-:1      LOP.AND.NZ P4, RZ, flag, 0x1;
--:-:-:-:1      MOV dimA, gridDimA;
03:-:-:-:1      ISCADD tbid, blkA, tid, 8;
04:-:-:-:1      XMAD.U16.U16 dimA, blkB, dimA, RZ;
--:-:-:-:1      ISCADD tbid, dimA, tbid, 8;
--:-:-:-:1      LOP.AND seed, tbid, 1x<2048*32 - 1>;
--:-:-:-:1      LEA      Rand0.CC, seed, param_Rand[0],     0x2;
--:-:-:-:1      LEA.HI.X Rand1,    seed, param_Rand[1], RZ, 0x2;
--:-:-:-:1  @P4 LDG.E.CS seed, [Rand];

--:-:-:-:1      LOP.AND tid1,   tid,  1;
--:-:-:-:1      LOP.AND tid96,  tid,  96;
--:-:-:-:1      LOP.AND tid128, tid,  128;

<CODE>
    our $vec;
    return $vec ? q{

01:-:-:-:1      ISETP.LT.AND P0, PT, tid, 128, PT;
06:-:-:-:1      SEL blk, blkA, blkB, P0;
--:-:-:-:1  @P0 MOV lda,  param_lda;
--:-:-:-:1 @!P0 MOV lda,  param_ldb;
--:-:-:-:1      SHL lda8, lda, 4;

// tidX = (tid & 15) << 3
// tidY = (tid >> 4) & 7
--:-:-:-:1      LOP.AND tid31,  tid,  31;
--:-:-:-:1      LOP.AND tid15,  tid,  15;
--:-:-:-:1      SHL     tidX,   tid15, 3;
--:-:-:-:1      BFE.U32 tidY,   tid,  0x304; // 3 bits at position 4

// trackX += (blkX*128 + lda*tidY + tidX) * 2
--:-:-:-:1      ISCADD   txa, blk, tidX, 7;
--:-:-:-:1      XMAD.LO2 ta, lda, tidY, txa;
--:-:-:-:1  @P0 LEA      trackX0.CC, ta, param_A[0],     0x1;
--:-:-:-:1  @P0 LEA.HI.X trackX1,    ta, param_A[1], RZ, 0x1;
--:-:-:-:1 @!P0 LEA      trackX0.CC, ta, param_B[0],     0x1;
--:-:-:-:0 @!P0 LEA.HI.X trackX1,    ta, param_B[1], RZ, 0x1;

--:-:-:-:1  @P0 ISETP.LT.AND P5, PT, txa, param_m, PT;
--:-:-:-:1 @!P0 ISETP.LT.AND P5, PT, txa, param_n, PT;

// writeS = (128*tidY + tidX) * 4
// writeS += 4x<128*8> if tid >= 128
--:-:-:-:1      ISCADD  writeS, tidY, tidX, 7;
--:-:-:-:1      SHL     writeS, writeS, 2;
--:-:-:-:1 @!P0 IADD    writeS, writeS, 4x<128*8>;

    // Vec 4 and scalar loads
    } : q{

// tidX = (tid & 31) << 2
// tidY = (tid >> 5) & 7
01:-:-:-:1      LOP.AND tid31,  tid,  31;
--:-:-:-:1      SHL     tidX,   tid31, 2;
--:-:-:-:1      BFE.U32 tidY,   tid,  0x305; // 3 bits at position 5

--:-:-:-:1      MOV lda,  param_lda;
--:-:-:-:1      MOV ldb,  param_ldb;
--:-:-:-:1      SHL lda8, lda, 4;
--:-:-:-:1      SHL ldb8, ldb, 4;

// trackA += (blkA*128 + lda*tidY + tidX) * 2
02:-:-:-:1      ISCADD   txa, blkA, tidX, 7;
--:-:-:-:1      XMAD.LO2 ta,  lda,  tidY, txa;
--:-:-:-:1      LEA      trackA0.CC, ta, param_A[0],     0x1;
--:-:-:-:0      LEA.HI.X trackA1,    ta, param_A[1], RZ, 0x1;

--:-:-:-:1      ISETP.LT.AND P5, PT, txa, param_m, PT;

// trackB += (blkB*128 + ldb*tidY + tidX) * 2
04:-:-:-:1      ISCADD   txb, blkB, tidX, 7;
--:-:-:-:1      XMAD.LO2 tb,  ldb,  tidY, txb;
--:-:-:-:1      LEA      trackB0.CC, tb, param_B[0],     0x1;
--:-:-:-:0      LEA.HI.X trackB1,    tb, param_B[1], RZ, 0x1;

--:-:-:-:1      ISETP.LT.AND P6, PT, txb, param_n, PT;

// writeS = (128*tidY + tidX) * 4
--:-:-:-:1      ISCADD  writeS, tidY, tidX, 7;
--:-:-:-:1      SHL     writeS, writeS, 2;
    };
</CODE>

--:-:-:-:1      LOP.XOR writeS, writeS, 4x<128*8*2>;

// readAs  = (((tid & 0x70) >> 3) | (tid & 1)) << 4
--:-:-:-:1      LOP.AND readAs, tid,    0x70;
--:-:-:-:1      SHR.U32 readAs, readAs, 3;
--:-:-:-:1      LOP.OR  readAs, readAs, tid1;
--:-:-:-:1      SHL     readAs, readAs, 4;

// readBs = ((tid128 >> 4) | ((tid >> 1) & 7)) << 4 + 4096;
--:-:-:-:1      BFE.U32 tid7,   tid,    0x301; // 3 bits at position 1
--:-:-:-:1      SHR.U32 readBs, tid128, 4;
--:-:-:-:1      LOP.OR  readBs, readBs, tid7;
--:-:-:-:1      ISCADD  readBs, readBs, 4x<128*8>, 4;
</SCHEDULE_BLOCK>

<CODE>
    return join '', map sprintf("--:-:1:-:1      LDS.U.128 czero%02d, [addr_zero];\n", $_ * 4), 0..15;
</CODE>

REMAINDER:

<SCHEDULE_BLOCK>

<CODE>
    our $vec;
    return $vec ? q{

// doLoad = tidY < k && txa|txb < n|m
--:-:-:-:1      ISETP.LT.AND P2, PT, tidY, k, P5;

--:-:2:-:1  @P2 LDG.E.CI.128 loadX, [trackX];
--:-:3:-:2 @!P2 LDS.U.128 loadX, [addr_zero];

    // Vec 4 and scalar loads
    } : q{

// doLoadA = tidY < k && txa < m
// doLoadB = tidY < k && txb < n
--:-:-:-:1      IADD x1, txa, 1;
--:-:-:-:1      IADD x2, txa, 2;
--:-:-:-:1      IADD x3, txa, 3;
--:-:-:-:1      ISETP.LT.AND P0, PT, tidY, k, P5;
--:-:-:-:1      ISETP.LT.AND P1, PT, x1, param_m, P0;
--:-:-:-:1      ISETP.LT.AND P2, PT, x2, param_m, P0;
--:-:-:-:1      ISETP.LT.AND P3, PT, x3, param_m, P0;

--:-:2:-:1  @P0 LDG.E.CI.S16 loadA0, [trackA + 2x<00 + 0>];
--:-:2:-:1  @P1 LDG.E.CI.S16 loadA1, [trackA + 2x<00 + 1>];
--:-:2:-:1  @P2 LDG.E.CI.S16 loadA2, [trackA + 2x<00 + 2>];
--:-:2:-:1  @P3 LDG.E.CI.S16 loadA3, [trackA + 2x<00 + 3>];

--:-:-:-:1 @!P0 MOV loadA0, RZ;
--:-:-:-:1 @!P1 MOV loadA1, RZ;
--:-:-:-:1 @!P2 MOV loadA2, RZ;
--:-:-:-:1 @!P3 MOV loadA3, RZ;

--:-:-:-:1      IADD y1, txb, 1;
--:-:-:-:1      IADD y2, txb, 2;
--:-:-:-:1      IADD y3, txb, 3;
--:-:-:-:1      ISETP.LT.AND P0, PT, tidY, k, P6;
--:-:-:-:1      ISETP.LT.AND P1, PT, y1, param_n, P0;
--:-:-:-:1      ISETP.LT.AND P2, PT, y2, param_n, P0;
--:-:-:-:1      ISETP.LT.AND P3, PT, y3, param_n, P0;

--:-:3:-:1  @P0 LDG.E.CI.S16 loadB0, [trackB + 2x<00 + 0>];
--:-:3:-:1  @P1 LDG.E.CI.S16 loadB1, [trackB + 2x<00 + 1>];
--:-:3:-:1  @P2 LDG.E.CI.S16 loadB2, [trackB + 2x<00 + 2>];
--:-:3:-:1  @P3 LDG.E.CI.S16 loadB3, [trackB + 2x<00 + 3>];

--:-:-:-:1 @!P0 MOV loadB0, RZ;
--:-:-:-:1 @!P1 MOV loadB1, RZ;
--:-:-:-:1 @!P2 MOV loadB2, RZ;
--:-:-:-:1 @!P3 MOV loadB3, RZ;

    };
</CODE>

</SCHEDULE_BLOCK>

<CODE>
    our $vec;
    return $vec ? q{
// bDoRemainder = k & 7 && k > 8
--:-:-:-:0      LOP.AND.NZ P1, RZ, k, 7;

06:-:-:-:4      F2F.F32.F16 loadX7, loadX3.H1;
--:-:-:-:4      F2F.F32.F16 loadX6, loadX3;
--:-:-:-:4      F2F.F32.F16 loadX5, loadX2.H1;
--:-:2:-:4      F2F.F32.F16 loadX4, loadX2;

--:-:-:-:0      IADD   trackX0.CC, trackX0, lda8;

--:-:-:-:4      F2F.F32.F16 loadX3, loadX1.H1;
--:-:-:-:4      F2F.F32.F16 loadX2, loadX1;
--:-:-:-:4      F2F.F32.F16 loadX1, loadX0.H1;
--:-:-:-:0      ISETP.GT.AND P1, PT, k, 8, P1;
--:-:3:-:1      F2F.F32.F16 loadX0, loadX0;

--:-:-:-:0      IADD.X trackX1, trackX1, RZ;

02:-:-:-:1      STS.128 [writeS + 4x<4>], loadX4;
04:-:-:-:1      STS.128 [writeS + 4x<0>], loadX0;

    // scalar loads
    } : q{
--:-:-:-:0      ISETP.GT.AND P1, PT, k, 8, PT;

02:-:-:-:4      F2F.F32.F16 loadA0, loadA0;
--:-:-:-:0      IADD   trackA0.CC, trackA0, lda8;
--:-:-:-:4      F2F.F32.F16 loadA1, loadA1;
--:-:-:-:4      F2F.F32.F16 loadA2, loadA2;
--:-:2:-:2      F2F.F32.F16 loadA3, loadA3;

--:-:-:-:0      IADD.X trackA1, trackA1, RZ;

02:-:-:-:1      STS.128 [writeS + 4x<0*128>], loadA0;

04:-:-:-:4      F2F.F32.F16 loadB0, loadB0;
--:-:-:-:0      IADD   trackB0.CC, trackB0, ldb8;
--:-:-:-:4      F2F.F32.F16 loadB1, loadB1;
--:-:-:-:4      F2F.F32.F16 loadB2, loadB2;
--:-:3:-:2      F2F.F32.F16 loadB3, loadB3;

--:-:-:-:0      IADD.X trackB1, trackB1, RZ;

04:-:-:-:1      STS.128 [writeS + 4x<8*128>], loadB0;

    };
</CODE>

--:-:-:-:1      LOP.XOR readAs, readAs, 4x<128*8*2>;
--:-:-:-:0      LOP.XOR readBs, readBs, 4x<128*8*2>;
01:-:-:-:5      BAR.SYNC 0;
--:-:-:-:0      LOP.XOR writeS, writeS, 4x<128*8*2>;


<CODE>
    our $vec;
    my $k_end = $vec ? 16 : 24;
    our @top = ("--:-:-:-:1      ISETP.GE.AND P2, PT, k, $k_end, P5;\n");

    our %insert =
    (
        j0c8  => "--:-:-:-:1      ISETP.GE.AND P0, PT, k, $k_end, PT;\n",

        ($vec ? 
            (
        j0c11 => "--:-:2:-:1  \@P2 LDG.E.CI.128 loadX, [trackX];\n",

        j5c1  => "02:-:-:-:1  \@P2 F2F.F32.F16 loadX7, loadX3.H1;\n",
        j5c5  => "--:-:-:-:1  \@P2 F2F.F32.F16 loadX6, loadX3;\n",
        j5c9  => "--:-:-:-:1  \@P2 F2F.F32.F16 loadX5, loadX2.H1;\n",
        j5c13 => "--:-:2:-:1  \@P2 F2F.F32.F16 loadX4, loadX2;\n",

        j5c31 => "02:-:-:-:1  \@P0 STS.128 [writeS + 4x<4>], loadX4;\n",

        j6c1  => "--:-:-:-:1  \@P2 F2F.F32.F16 loadX3, loadX1.H1;\n",
        j6c5  => "--:-:-:-:1  \@P2 F2F.F32.F16 loadX2, loadX1;\n",
        j6c9  => "--:-:-:-:1  \@P2 F2F.F32.F16 loadX1, loadX0.H1;\n",
        j6c13 => "--:-:2:-:1  \@P2 F2F.F32.F16 loadX0, loadX0;\n",

        j6c31 => "02:-:-:-:1  \@P0 STS.128 [writeS + 4x<0>], loadX0;\n",

        j6c46 => "--:-:-:-:1  \@P2 IADD   trackX0.CC, trackX0, lda8;\n",
        j6c54 => "--:-:-:-:1  \@P2 IADD.X trackX1,    trackX1, RZ;\n",
            ) : 
            (
        j0c7  => "--:-:-:-:1      ISETP.GE.AND P3, PT, k, $k_end, P6;\n",

        j0c10 => "--:-:2:-:1  \@P2 LDG.E.CI.S16 loadA0, [trackA + 2x<0>];\n",
        j0c29 => "--:-:2:-:1  \@P2 LDG.E.CI.S16 loadA1, [trackA + 2x<1>];\n",
        j0c31 => "--:-:2:-:1  \@P2 LDG.E.CI.S16 loadA2, [trackA + 2x<2>];\n",
        j0c33 => "--:-:2:-:1  \@P2 LDG.E.CI.S16 loadA3, [trackA + 2x<3>];\n",

        j0c35 => "--:-:4:-:1  \@P3 LDG.E.CI.S16 loadB0, [trackB + 2x<0>];\n",
        j1c29 => "--:-:4:-:1  \@P3 LDG.E.CI.S16 loadB1, [trackB + 2x<1>];\n",
        j1c31 => "--:-:4:-:1  \@P3 LDG.E.CI.S16 loadB2, [trackB + 2x<2>];\n",
        j1c33 => "--:-:4:-:1  \@P3 LDG.E.CI.S16 loadB3, [trackB + 2x<3>];\n",

        j5c8  => "02:-:-:-:1  \@P2 F2F.F32.F16 loadA0, loadA0;\n",
        j5c12 => "--:-:-:-:1  \@P2 F2F.F32.F16 loadA1, loadA1;\n",
        j5c16 => "--:-:-:-:1  \@P2 F2F.F32.F16 loadA2, loadA2;\n",
        j5c20 => "--:-:2:-:1  \@P2 F2F.F32.F16 loadA3, loadA3;\n",

        j6c8  => "08:-:-:-:1  \@P3 F2F.F32.F16 loadB0, loadB0;\n",
        j6c12 => "--:-:-:-:1  \@P3 F2F.F32.F16 loadB1, loadB1;\n",
        j6c16 => "--:-:-:-:1  \@P3 F2F.F32.F16 loadB2, loadB2;\n",
        j6c20 => "--:-:4:-:1  \@P3 F2F.F32.F16 loadB3, loadB3;\n",

        j5c39 => "02:-:-:-:1  \@P0 STS.128 [writeS + 4x<0*128>], loadA0;\n",

        j5c46 => "--:-:-:-:1  \@P2 IADD   trackA0.CC, trackA0, lda8;\n",
        j5c54 => "--:-:-:-:1  \@P2 IADD.X trackA1,    trackA1, RZ;\n",

        j6c39 => "08:-:-:-:1  \@P0 STS.128 [writeS + 4x<8*128>], loadB0;\n",

        j6c46 => "--:-:-:-:1  \@P3 IADD   trackB0.CC, trackB0, ldb8;\n",
        j6c54 => "--:-:-:-:1  \@P3 IADD.X trackB1,    trackB1, RZ;\n",
            )
        ),

        j6c63 => "--:-:-:-:5  \@P0 BAR.SYNC 0;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR readAs, readAs, 4x<128*8*2>;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR readBs, readBs, 4x<128*8*2>;\n" .
                 "--:-:-:-:1  \@P0 LOP.XOR writeS, writeS, 4x<128*8*2>;\n" .
                 "--:-:-:-:1      IADD32I k, k, -8;\n",

        j7c63 => "--:-:-:Y:5  \@P0 BRA.U LOOP;\n" .
                 "--:-:-:Y:5  \@P1 BRA.U REMAINDER;\n",
    );
    return;
</CODE>

<INCLUDE file="nervanagpu/kernels/sass/hgemm_common_128x128.sass"/>
